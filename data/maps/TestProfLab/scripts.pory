mapscripts TestProfLab_MapScripts{
    MAP_SCRIPT_ON_TRANSITION:
        TestProfLab_ControlScript_ControlState
}

script TestProfLab_ControlScript_ControlState{
    removeobject(5)
    setvar(VAR_TESTPROFLAB_CONTROLSTATE, 0)
    call(Common_EventScript_SetupRivalGfxId)
}

script TestProfLab_PersonScript_TestProf{
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        lock
        faceplayer
        message("Hello, would you like to see more?")
        multichoice(25, 5, MULTI_STARTER_CHOICE, 0)
        closemessage
        if(var(VAR_RESULT) == 0){
            call(TestProfLab_ActionScript_StarterFade)
            setvar(VAR_TESTPROFLAB_CONTROLSTATE, 2)
        }
        elif(var(VAR_RESULT) == 1){
            call(TestProfLab_ActionScript_StarterFade)
            setvar(VAR_TESTPROFLAB_CONTROLSTATE, 1)
        }
        elif(var(VAR_RESULT) == 2){
            call(TestProfLab_ActionScript_StarterFade)
            setvar(VAR_TESTPROFLAB_CONTROLSTATE, 0)
        }
        msgbox("Choose wisely...")
        waitmessage
        release
    }
    else{
        lock
        faceplayer
        if(flag(FLAG_SYS_POKEDEX_GET) && !(flag(FLAG_SYS_NATIONAL_DEX))){
            msgbox(format("Do you want to upgrade to the National Dex?"), MSGBOX_YESNO)
            if(var(VAR_RESULT) == YES){
                call(TestProfLab_ActionScript_UpgradeToNatDex)
            }
            else{
                msgbox("Please take care of your Pokemon.", MSGBOX_NPC)
            }
        }
        else{
            msgbox("Please take care of your Pokemon.", MSGBOX_NPC)
        }
        release
    }
    end    
}

script TestProfLab_PersonScript_BallChangeAide{
    lock
    faceplayer
    msgbox(format("Do you want me to change the Ball Type your Pokemon is in?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        message(format("Ok, great! What would you like it changed to?"))
        multichoice(25, 4, MULTI_BALL_CHANGE, 0)
        closemessage
        if(var(VAR_RESULT) == 0){
            setmonball(ITEM_DUSK_BALL)
        }
        elif(var(VAR_RESULT) == 1){
            setmonball(ITEM_QUICK_BALL)
        }
        elif(var(VAR_RESULT) == 2){
            setmonball(ITEM_ULTRA_BALL)
        }
        elif(var(VAR_RESULT) == 3){
            setmonball(ITEM_DIVE_BALL)
        }
        msgbox("All set! Come again...", MSGBOX_DEFAULT)
    }
    elif(var(VAR_RESULT) == NO){
        msgbox("If you change your mind, I'm here.", MSGBOX_DEFAULT)
    }
    release
    end
}

script TestProfLab_ObjectScript_PokeballOne{
    lock
    setvar(VAR_TEMP_0, 1)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        if(var(VAR_TESTPROFLAB_CONTROLSTATE) == 0){
            bufferspeciesname(STR_VAR_1, SPECIES_TREECKO)
            setvar(VAR_STARTER_CHOICE, SPECIES_TREECKO)
            call(TestProfLab_ActionScript_GiveStarter)
        }
        elif(var(VAR_TESTPROFLAB_CONTROLSTATE) == 1){
            bufferspeciesname(STR_VAR_1, SPECIES_CHIKORITA)
            setvar(VAR_STARTER_CHOICE, SPECIES_CHIKORITA)
            call(TestProfLab_ActionScript_GiveStarter)
        }
        elif(var(VAR_TESTPROFLAB_CONTROLSTATE) == 2){
            bufferspeciesname(STR_VAR_1, SPECIES_BULBASAUR)
            setvar(VAR_STARTER_CHOICE, SPECIES_BULBASAUR)
            call(TestProfLab_ActionScript_GiveStarter)
        }
    }
    else{
        msgbox("Don't Take Another.")
    }
    waitmessage
    release
    end
}

script TestProfLab_ObjectScript_PokeballTwo{
    lock
    setvar(VAR_TEMP_0, 2)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        if(var(VAR_TESTPROFLAB_CONTROLSTATE) == 0){
            bufferspeciesname(STR_VAR_1, SPECIES_MUDKIP)
            setvar(VAR_STARTER_CHOICE, SPECIES_MUDKIP)
            call(TestProfLab_ActionScript_GiveStarter)
        }
        elif(var(VAR_TESTPROFLAB_CONTROLSTATE) == 1){
            bufferspeciesname(STR_VAR_1, SPECIES_TOTODILE)
            setvar(VAR_STARTER_CHOICE, SPECIES_TOTODILE)
            call(TestProfLab_ActionScript_GiveStarter)
        }
        elif(var(VAR_TESTPROFLAB_CONTROLSTATE) == 2){
            bufferspeciesname(STR_VAR_1, SPECIES_SQUIRTLE)
            setvar(VAR_STARTER_CHOICE, SPECIES_SQUIRTLE)
            call(TestProfLab_ActionScript_GiveStarter)
        }
    }
    else{
        msgbox("Don't Take Another.")
    }
    waitmessage
    release
    end
}

script TestProfLab_ObjectScript_PokeballThree{
    lock
    setvar(VAR_TEMP_0, 3)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        if(var(VAR_TESTPROFLAB_CONTROLSTATE) == 0){
            bufferspeciesname(STR_VAR_1, SPECIES_TORCHIC)
            setvar(VAR_STARTER_CHOICE, SPECIES_TORCHIC)
            call(TestProfLab_ActionScript_GiveStarter)
        }
        elif(var(VAR_TESTPROFLAB_CONTROLSTATE) == 1){
            bufferspeciesname(STR_VAR_1, SPECIES_CYNDAQUIL)
            setvar(VAR_STARTER_CHOICE, SPECIES_CYNDAQUIL)
            call(TestProfLab_ActionScript_GiveStarter)
        }
        elif(var(VAR_TESTPROFLAB_CONTROLSTATE) == 2){
            bufferspeciesname(STR_VAR_1, SPECIES_CHARMANDER)
            setvar(VAR_STARTER_CHOICE, SPECIES_CHARMANDER)
            call(TestProfLab_ActionScript_GiveStarter)
        }
    }
    else{
        msgbox("Don't Take Another.")
    }
    waitmessage
    release
    end
}

script TestProfLab_TrainerScript_MayBattle{
    playbgm(MUS_ENCOUNTER_MAY, TRUE)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(5, TestProfLab_MovementScript_RivalMoveUp)
    waitmovement(0)
    trainerbattle_single(TRAINER_MAY_ROUTE_103_TREECKO, TestProfLab_TextScript_RivalIntro, Route103_Text_MayDefeated, TestProfLab_ActionScript_RivalAftermath, FALSE)
    end
}

script TestProfLab_TrainerScript_BrendanBattle{
    playbgm(MUS_ENCOUNTER_MAY, TRUE)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(5, TestProfLab_MovementScript_RivalMoveUp)
    waitmovement(0)
    trainerbattle_single(TRAINER_BRENDAN_ROUTE_103_TREECKO, TestProfLab_TextScript_RivalIntro, Route103_Text_BrendanDefeated, TestProfLab_ActionScript_RivalAftermath, FALSE)
    end
}

script TestProfLab_ActionScript_GiveStarter{
    showmonpic(VAR_STARTER_CHOICE, 10, 3)
    msgbox("Do you want to choose {STR_VAR_1}?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        setflag(FLAG_SYS_POKEMON_GET)
        setvar(VAR_LITTLEROOT_STATE, 2)
        givemon(VAR_STARTER_CHOICE, 10, ITEM_ORAN_BERRY)
        call(TestProfLab_ActionScript_BallToRemove)
        if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("Received {STR_VAR_1}!")
            call(TestProfLab_ActionScript_SetBallFlag)
            call(TestProfLab_ActionScript_NicknamePartyMon)
        }
        elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("Received {STR_VAR_1}!")
            call(TestProfLab_ActionScript_SetBallFlag)
            call(TestProfLab_ActionScript_NicknameBoxMon)
            msgbox("Transferred {STR_VAR_1} to PC")
        }
        else{
            hidemonpic
            msgbox("No more room for Pokemon!")
        }
        closemessage
        call(TestProfLab_ActionScript_InitiateDexAndRivalSequence)
    }
    else{
        hidemonpic
        msgbox(TestProfLab_TextScript_NotChosen)
    }
}

script TestProfLab_ActionScript_BallToRemove{
    if(var(VAR_TEMP_0) == 1){
        removeobject(2)
        return
    }
    if(var(VAR_TEMP_0) == 2){
        removeobject(3)
        return
    }
    if(var(VAR_TEMP_0) == 3){
        removeobject(4)
        return
    }
}

script TestProfLab_ActionScript_SetBallFlag{
    if(var(VAR_TEMP_0) == 1){
        setflag(FLAG_PROF_BALL_1)
        return
    }
    if(var(VAR_TEMP_0) == 2){
        setflag(FLAG_PROF_BALL_2)
        return
    }
    if(var(VAR_TEMP_0) == 3){
        setflag(FLAG_PROF_BALL_3)
        return
    }
}

script TestProfLab_ActionScript_InitiateDexAndRivalSequence{
    lock
    delay(20)
    applymovement(OBJ_EVENT_ID_PLAYER, TestProfLab_MovementScript_TurnToProf)
    if(var(VAR_TEMP_0) == 1){
        setobjectxy(5, 5, 11)
        applymovement(1, TestProfLab_MovementScript_MoveBallOne)
    }
    if(var(VAR_TEMP_0) == 2){
        setobjectxy(5, 6, 11)
        applymovement(1, TestProfLab_MovementScript_MoveBallTwo)
    }
    if(var(VAR_TEMP_0) == 3){
        setobjectxy(5, 7, 11)
        applymovement(1, TestProfLab_MovementScript_MoveBallThree)
    }
    waitmovement(0)
    delay(20)
    call(TestProfLab_ActionScript_GivePokedex)
    call(TestProfLab_ActionScript_InitRivalSequence)
    release
    return
}

script TestProfLab_ActionScript_GivePokedex{
    lock
    msgbox(TestProfLab_TextScript_TakePokedex, MSGBOX_DEFAULT)
    playfanfare(MUS_OBTAIN_ITEM)
    message("{PLAYER} received the Pokedex!")
    waitfanfare
    closemessage
    setflag(FLAG_SYS_POKEDEX_GET)
    setflag(FLAG_ADVENTURE_STARTED)
    special(SetUnlockedPokedexFlags)
    setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
    setvar(VAR_CABLE_CLUB_TUTORIAL_STATE, 1)
    msgbox(LittlerootTown_ProfessorBirchsLab_Text_ExplainPokedex, MSGBOX_DEFAULT)
    msgbox("Also take these with you...", MSGBOX_DEFAULT)
    giveitem(ITEM_POKE_BALL, 5)
    msgbox(format("These allow you to catch your own Pokemon in tall grass so you can collect research for the Pokedex."), MSGBOX_DEFAULT)
    waitmessage
    release
    return
}

script TestProfLab_ActionScript_InitRivalSequence{
    checkplayergender
    goto_if_eq(VAR_RESULT, MALE, TestProfLab_TrainerScript_MayBattle)
    goto_if_eq(VAR_RESULT, FEMALE, TestProfLab_TrainerScript_BrendanBattle)
}

script TestProfLab_ActionScript_RivalAftermath{
    lockall
    msgbox(format("Well, that wasn't fun. I'll beat you next time!"))
    applymovement(5, TestProfLab_MovementScript_RivalLeave)
    waitmovement(0)
    setflag(FLAG_TEST_BRENDAN)
    setvar(VAR_LITTLEROOT_STATE, 3)
    removeobject(5)
    fadedefaultbgm
    releaseall
}

script TestProfLab_ActionScript_UpgradeToNatDex{
    lock
    playfanfare(MUS_OBTAIN_ITEM)
    message(LittlerootTown_ProfessorBirchsLab_Text_PokedexUpgradedToNational)
    waitmessage
    waitfanfare
    setflag(FLAG_SYS_NATIONAL_DEX)
    special(EnableNationalPokedex)
    closemessage
    release
    return
}

script TestProfLab_ActionScript_StarterFade{
    fadescreen(FADE_TO_BLACK)
    delay(20)
    fadescreen(FADE_FROM_BLACK)
    return
}

script TestProfLab_ActionScript_NicknamePartyMon{
    msgbox("Give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    } 
}

script TestProfLab_ActionScript_NicknameBoxMon{
    msgbox("Give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_NameReceivedBoxMon)
    }
    call(Common_EventScript_TransferredToPC)
}

script TestProfLab_TriggerScript_StartStarterSequence{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, TestProfLab_MovementScript_MeetTestProf)
    waitmovement(0)
    msgbox(TestProfLab_TextScript_BeginAdventureText)
    waitmessage
    closemessage
    applymovement(1, TestProfLab_MovementScript_ProfMoves)
    waitmovement(0)
    setvar(VAR_LITTLEROOT_STATE, 1)
    release
}

script TestProfLab_TriggerScript_StartStarterSequence2{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, TestProfLab_MovementScript_MeetTestProf2)
    waitmovement(0)
    goto(TestProfLab_TriggerScript_StartStarterSequence)
}

script TestProfLab_TriggerScript_StopLeaving{
    lockall
    msgbox("You need to choose a Pokemon!")
    applymovement(OBJ_EVENT_ID_PLAYER, TestProfLab_MovementScript_DoNotLeave)
    waitmovement(0)
    releaseall
}

movement TestProfLab_MovementScript_MeetTestProf{
    walk_up * 4
}

movement TestProfLab_MovementScript_MeetTestProf2{
    walk_left
}

movement TestProfLab_MovementScript_ProfMoves{
    walk_up
    walk_right * 2
    walk_up
    face_down
}

movement TestProfLab_MovementScript_MoveBallOne{
    walk_left * 2
}

movement TestProfLab_MovementScript_MoveBallTwo{
    walk_left * 1
}

movement TestProfLab_MovementScript_MoveBallThree{
    face_left
}

movement TestProfLab_MovementScript_RivalMoveUp{
    walk_up * 6
}

movement TestProfLab_MovementScript_RivalLeave{
    walk_down * 6
}

movement TestProfLab_MovementScript_TurnToProf{
    face_right
}

movement TestProfLab_MovementScript_DoNotLeave{
    walk_up
}

text TestProfLab_TextScript_BeginAdventureText{
    "{COLOR RED}{SHADOW LIGHT_RED}TestProf:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Hello {PLAYER}, my name is\n"
    "TestProf. This is my lab.\p"
    "Today is the day that you will be\n"
    "receiving a Pokemon.\p"
    "{COLOR GREEN}{SHADOW LIGHT_GREEN}Now choose wisely.\n"
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}If you would like to see more options\l"
    "come and talk to me."
}

text TestProfLab_TextScript_TakePokedex{
    "Here, {PLAYER}, I ordered this for my\n"
    "research, but I think you should have\l"
    "this Pokedex."
}

text TestProfLab_TextScript_NotChosen{
    "You did not choose {STR_VAR_1}."
}

text TestProfLab_TextScript_RivalIntro{
    "We'll see if you have what it takes!"
}
